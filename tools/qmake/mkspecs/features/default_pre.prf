# Resolve root directories for sources
QTWEBENGINE_ROOT = $$replace(PWD, /tools/qmake/mkspecs/features$,)

# We depend on libc++ to build chromium so our macosx-version-min has to be 10.7
QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.7

QTWEBENGINEPROCESS_NAME_RELEASE = QtWebEngineProcess
debug_and_release {
    QTWEBENGINEPROCESS_NAME_DEBUG = $$join(QTWEBENGINEPROCESS_NAME_RELEASE,,,d)
} else {
    QTWEBENGINEPROCESS_NAME_DEBUG = $$QTWEBENGINEPROCESS_NAME_RELEASE
}
build_pass:CONFIG(debug, debug|release) {
    QTWEBENGINEPROCESS_NAME = $$QTWEBENGINEPROCESS_NAME_DEBUG
} else {
    QTWEBENGINEPROCESS_NAME = $$QTWEBENGINEPROCESS_NAME_RELEASE
}

# Location of sync.profile
MODULE_BASE_DIR = $$QTWEBENGINE_ROOT

EOL = $$escape_expand(\\n)

# Call the original default_pre.
load(default_pre)
load(functions)

# Check platform support and run config tests early enough to bail
equals(_PRO_FILE_, "$$QTWEBENGINE_ROOT/qtwebengine.pro"): CONFIG += root_project_file

root_project_file:isPlatformSupported() {
   !exists($$QTWEBENGINE_ROOT/src/3rdparty/chromium) {
       error("Submodule qtwebengine-chromium does not exist. Run 'git submodule update --init'.")
   }
   load(configure)
   runConfigure()
}
