# Resolve root directories for sources
QTWEBENGINE_ROOT = $$replace(PWD, /build/qmake/mkspecs/features$,)

QTWEBENGINEPROCESS_NAME = QtWebEngineProcess

git_chromium_src_dir = $$system("git config qtwebengine.chromiumsrcdir")

# Fall back to the snapshot path if git does not know about chromium sources (i.e. init-repository.py has not been used)
isEmpty(git_chromium_src_dir): git_chromium_src_dir = "3rdparty/chromium"

CHROMIUM_SRC_DIR = $$absolute_path("$$QTWEBENGINE_ROOT/$$git_chromium_src_dir")

INCLUDEPATH += $$CHROMIUM_SRC_DIR

# Used for our export macros
!contains(_PRO_FILE_PWD_, $$QTWEBENGINE_ROOT/examples): DEFINES += BUILDING_CHROMIUM

# Location of sync.profile
MODULE_BASE_DIR = $$QTWEBENGINE_ROOT

# We have to disable RTTI for now since that's how chromium builds on linux
unix:QMAKE_CXXFLAGS += -fno-rtti

load(functions)

# Call the original default_pre.
load(default_pre)
